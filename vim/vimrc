" vim: sw=2 ts=2 sts=2 et fmr={{{,}}} fdm=marker

" Configurable through the constant g:installType#isCompleteInstall in
" vim/autoload/installType.vim.
" The constant controls which plugins are installed, either all plugins or a
" smaller subset which are not necessary on some machines (e.g. servers).

" Plug setup {{{
  call plug#begin('~/.vim/plugged')
  Plug 'chriskempson/base16-vim'
  Plug 'bling/vim-airline'
  Plug 'vim-airline/vim-airline-themes'

  " Fuzzy file finding
  Plug 'ctrlpvim/ctrlp.vim'

  Plug 'scrooloose/nerdtree', {'on': ['NERDTree', 'NERDTreeFocus',
              \'NERDTreeToggle']}

  " autoclosing of brackets, quotes, etc.
  Plug 'Raimondi/delimitMate'

  " make surroundings repeatable
  Plug 'tpope/vim-repeat'
  " motions for surrounding text objects with brackets, quotes, etc.
  Plug 'tpope/vim-surround'

  " commands for toggling of comments
  Plug 'tomtom/tcomment_vim'

  " git plugin
  Plug 'tpope/vim-fugitive'

  " Show git changes in the gutter (left of line numbers)
  Plug 'airblade/vim-gitgutter', {'on': ['GitGutterToggle']}

  " dependency for vim-session
  Plug 'xolox/vim-misc'
  " Nice session management
  Plug 'xolox/vim-session'

  " also better highlights for c
  Plug 'octol/vim-cpp-enhanced-highlight'

  " sane buffer closing
  Plug 'vim-scripts/bufkill.vim'

  " improved syntax highlighting for javascript
  Plug 'pangloss/vim-javascript'

  if g:installType#isCompleteInstall == 1
    " Only rebuild YCM if necessary
    " From https://github.com/junegunn/vim-plug#post-update-hooks
    function! BuildYCM(info)
      " info is a dictionary with 3 fields
      " - name:   name of the plugin
      " - status: 'installed', 'updated', or 'unchanged'
      " - force:  set on PlugInstall! or PlugUpdate!
      if a:info.status == 'installed' || a:info.force
        !./install.py --clang-completer --tern-completer --gocode-completer
      endif
    endfunction

    Plug 'Valloric/YouCompleteMe', { 'do': function('BuildYCM') }

    " syntax checking
    Plug 'scrooloose/syntastic'

    " external deps: clang format
    Plug 'rhysd/vim-clang-format', {'for': ['c', 'cpp']}

    " syntax highlighting, autocomplete and more nice things for go
    Plug 'fatih/vim-go', {'for': 'go'}

    " external deps: ctags
    Plug 'majutsushi/tagbar'

    " essential for markdown editing. external deps: pandoc
    Plug 'vim-pandoc/vim-pandoc'
    Plug 'vim-pandoc/vim-pandoc-syntax'
    Plug 'godlygeek/tabular'

    " Snippets, yay
    Plug 'SirVer/UltiSnips'
    " UltiSnips does not contain any snippets, here are some maintained by the
    " community.
    Plug 'honza/vim-snippets'

    " Some useful additions for LaTeX editing
    Plug 'lervag/vimtex'

    " Swift syntax and indent files
    Plug 'kballard/vim-swift'

    " Plugin for ag (the_silver_searcher)
    Plug 'rking/ag.vim'
  endif
  call plug#end()
" }}}

" General {{{

  " disable the visual/error bells
  set noerrorbells
  " and enable visual bell, but clear the visual bell character
  set visualbell t_vb =

  " folding
  set foldenable
  set foldlevelstart=99
  set foldmethod=syntax

  " persistent undo
  set undofile
  set undodir=$HOME/.vimundo
" }}}

" UI {{{
  set title " Display the current file and path as the title
  set showcmd " show incomplete command in status bar

  set splitright " new vertical splits always to the right
  set splitbelow " new horizontal splits always below
  set laststatus=2 "always show the status line

  " better command line completion
  set wildmenu
  set wildmode=list:longest,full
" }}}

" Editor {{{
  syntax on
  set background=dark
  colorscheme base16-ocean

  set number " line numbers
  set ruler " position  of cursor
  set colorcolumn=+1 " highlight one column after text width
  set cursorline " highlight the line of the cursor

  " show tabs, trailing spaces, non-breaking spaces and line wraps
  set list
  set listchars=tab:›\ ,trail:•,precedes:…,extends:…,nbsp:.

  " Text format options. ':help fo-table' for detailed information
  " Text can be formatted with 'gq{motion}'.
  set formatoptions=tcroqnj
  " Formatting options lists. Vims default only formats ordered lists.
  " This adds unordered list beginning with *,-,+.
  set formatlistpat=^\\s*\\(\\d\\+[\\]:.)}\\t\ ]\\\|[*\\-\\+]\ \\)\\s*

  " Use system clipboard
  set clipboard+=unnamed

  if has("gui_running")
    if has('macunix')
        set guifont=Source\ Code\ Pro:h13
    endif

    if has("gui_macvim")
      " Make without automatic jump, open quickfix if error present.
      an 40.360 &Tools.&Make<Tab>:make :make!<CR>:cw<CR>
    endif

    set guioptions-=T " disable tab bar
    set guioptions-=m " disable menu
    set guioptions-=r " disable right scroll bar
    set guioptions-=R " disable right scroll bar
    set guioptions-=l " disable left scroll bar
    set guioptions-=L " disable left scroll bar
  endif
" }}}

" File Handling {{{
  set autoread
  set nobackup
  set noswapfile
" }}}

" Indentation {{{
  set shiftwidth=4
  set tabstop=4
  set softtabstop=4  " let backspace know about space indentation
  set expandtab
  set backspace=indent,eol,start
  set autoindent
  set copyindent
  set cindent
" }}}

" Search {{{
  set hlsearch
  set incsearch
  set ignorecase
  set smartcase
" }}}

" Functions {{{

  " Display the syntax highlight groups for word under cursor
  function! SynStack()
    if !exists('*synstack')
      return
    endif
    echo map(synstack(line('.'), col('.')), "synIDattr(v:val, 'name')")
  endfunc

 " Switch between dark and light color schemes
  function! s:toggleLightColorScheme()
    if &background ==? "light"
      set background=dark
      colorscheme base16-ocean
    else
      set background=light
      colorscheme base16-solarized
    endif
  endfunc
  command! ToggleLightColorScheme :call s:toggleLightColorScheme()

" }}}

" Key mappings {{{

  " better movement between windows
  nnoremap <C-h> <C-w>h
  nnoremap <C-j> <C-w>j
  nnoremap <C-k> <C-w>k
  nnoremap <C-l> <C-w>l

  " resize active window to 60% of the window width
  nnoremap <silent> <Leader>r :exe "vertical resize " .
        \ string(round(&columns * 0.60))<CR>

  " make j/k useful for long lines
  nnoremap j gj
  nnoremap k gk

  " center cursor on forward/backwards jumps
  nnoremap <C-b> <C-b>zz
  nnoremap <C-f> <C-f>zz

  " making saving easier
  nnoremap <silent> <Leader>w :<C-u>update<cr>

  " 'sudo save'
  cnoremap w!! w !sudo tee % >/dev/null

  " Newlines above/below current line without leaving normal mode
  nnoremap <silent> zk O<Esc>j
  nnoremap <silent> zj o<Esc>k

  " Keep selection when (re-)indenting
  vnoremap < <gv
  vnoremap > >gv
  vnoremap = =gv

  " centering searches (auto opens folds)
  nnoremap * *zzzv
  nnoremap # #zzzv
  nnoremap n nzzzv
  nnoremap N Nzzzv

  " reload vimrc
  nnoremap <F2> :source $MYVIMRC<cr>:echom "vimrc reloaded"<cr>

" }}}

" Plugins {{{

  " DelimitMate {{{
    " exclude `
    let delimitMate_quotes = "\" '"
    " put closing bracket on seperate line after enter
    let g:delimitMate_expand_cr = 1
  " }}}

  " NerdTree {{{
    let NERDTreeShowHidden = 1
    let NERDTreeIgnore = ['^\.git$', '^\.DS_Store$', '\.pyc$']

    noremap <silent> <Leader>o :NERDTreeToggle<CR>
  " }}}

  " Airline {{{
    " show current branch in status line (req. Fugitive)
    let g:airline#extensions#branch#enabled = 1

    " show buffers on the top of the window
    let g:airline#extensions#tabline#enabled = 1

    " quickly select open buffers (max 9)
    let g:airline#extensions#tabline#buffer_idx_mode = 1
    nmap <leader>1 <Plug>AirlineSelectTab1
    nmap <leader>2 <Plug>AirlineSelectTab2
    nmap <leader>3 <Plug>AirlineSelectTab3
    nmap <leader>4 <Plug>AirlineSelectTab4
    nmap <leader>5 <Plug>AirlineSelectTab5
    nmap <leader>6 <Plug>AirlineSelectTab6
    nmap <leader>7 <Plug>AirlineSelectTab7
    nmap <leader>8 <Plug>AirlineSelectTab8
    nmap <leader>9 <Plug>AirlineSelectTab9

    " Source Code Pro has the powerline symbols
    let g:airline_powerline_fonts = 1
  " }}}

  " GitGutter {{{
    " I don't want to always see git changes, only on demand.
    let g:gitgutter_enabled = 0

    nnoremap <leader>g :GitGutterToggle<CR>
  " }}}

  " Session {{{
    let g:session_autosave = "yes"
    let g:session_autoload = "no"
    let g:session_command_aliases = 1
  " }}}

  " Bufkill {{{
    nnoremap <silent> <Leader>bd :BD<CR>
  " }}}

  " CTRLP {{{
    " Include current file in search results. Otherwise I wonder where that
    " file went...
    let g:ctrlp_match_current_file = 1

    " Use ag if available
    if executable('ag')
      " Always use cwd as CtrlPs working directory
      let g:ctrlp_working_path_mode = 0
      let g:ctrlp_user_command = 'ag %s -l --nocolor --nogroup --hidden -g ""'
    endif
  " }}}

  if g:installType#isCompleteInstall == 1
    " YouCompleteMe {{{
      let g:ycm_global_ycm_extra_conf = "~/.vim/ycm_extra_conf.py"
      let g:ycm_autoclose_preview_window_after_insertion = 1

      nnoremap <silent> <Leader>yg :YcmCompleter GoTo<CR>
      nnoremap <silent> <Leader>yc :YcmCompleter GoToDeclaration<CR>
      nnoremap <silent> <Leader>yf :YcmCompleter GoToDefinition<CR>
    " }}}

    " ClangFormat {{{
      let g:clang_format#auto_format = 0
      let g:clang_format#auto_format_on_insert_leave = 0
      let g:clang_format#detect_style_file = 1
    " }}}

    " Tagbar {{{
      map <silent> <Leader>t :TagbarToggle<cr>
    " }}}

    " Syntastic {{{
      let g:syntastic_always_populate_loc_list = 1
      let g:syntastic_mode_map = { 'passive_filetypes': ['tex', 'swift'] }
    " }}}

    " Pandoc {{{
      let g:pandoc#syntax#conceal#use=0
      let g:pandoc#folding#fdc=0
      let g:pandoc#spell#enabled=0
      let g:pandoc#modules#disabled = ["chdir"]
    " }}}

    " UltiSnips {{{
      " TODO: Some other keybinding maybe?
      let g:UltiSnipsExpandTrigger="<c-l>"
      let g:UltiSnipsJumpForwardTrigger="<tab>"
      let g:UltiSnipsJumpBackwardTrigger="<s-tab>"
    " }}}

    " vimtex {{{
      " The folding interferes with diffing
      let g:vimtex_fold_enabled=0
      " let g:vimtex_view_method="skim"
      let g:vimtex_view_general_viewer
        \ = '/Applications/Skim.app/Contents/SharedSupport/displayline'
      let g:vimtex_view_general_options = '@line @pdf @tex'
    " }}}

    " swift {{{
      let g:swift_no_conceal=1
    " }}}

    " Ag {{{
      command! Todos :Ag \(FIXME\|TODO\)
    " }}}
  endif
" }}}

